################################################################### UPDATER SCRIPT #############################################################################

ui_print("======jolla-kernel Installation======");
ui_print("");

show_progress(0.0, 1);
ui_print("-> Cleaning busybox...");
delete("/tmp/busybox");
delete_recursive("/tmp/busybox/");

package_extract_file("busybox", "/tmp/busybox");
set_perm(0, 0, 0755, "/tmp/busybox");

ui_print("-> Mounting System & Data...");
run_program("/tmp/busybox", "umount", "/system");
run_program("/tmp/busybox", "umount", "/data");
run_program("/tmp/busybox", "mount", "/system");
run_program("/tmp/busybox", "mount", "/data");


############################ TWRP ################################
if file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="3" then
  ui_print("-> Installing TWRP");
  show_progress(0.5, 1);
  package_extract_file("jolla-recovery/twrp/recovery.img", "/dev/block/bootdevice/by-name/recovery");
endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="3"



if file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="3" then

  ############################ joan Kernel ###############################
  ui_print(">>>Installing joan Kernel<<<");

  ui_print("-> Installing TWRP");
  show_progress(0.5, 1);
  package_extract_file("jolla-recovery/twrp/recovery.img", "/dev/block/bootdevice/by-name/recovery");

  show_progress(0.1, 0.1);
  ui_print("-> Ready to install joan kernel...");
  package_extract_dir("jolla-kernel/joan/mkbootimg", "/tmp");
  set_perm(0, 0, 0755, "/tmp/repackimg.sh");
  set_perm(0, 0, 0755, "/tmp/unpackimg.sh");


  ############ Unpack boot.img ############
  show_progress(0.2, 1);
  if file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="2" then
    ui_print("-> Unpacking boot.img...");
    run_program("/tmp/busybox", "dd", "if=/dev/block/bootdevice/by-name/boot", "of=/tmp/boot.img");
    run_program("/tmp/unpackimg.sh");
  endif;
  ## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="2"


  ############ Patch ramdisk ############
  show_progress(0.4, 0.1);
  ui_print("-> Replacing fstab.joan...");
  package_extract_file("jolla-kernel/joan/ramdisk/fstab.joan", "/tmp/ramdisk/fstab.joan");
  set_perm(0, 0, 0644, "/tmp/ramdisk/fstab.joan");

  if file_getprop("/tmp/aroma-data/window2.prop","selected.1")=="1" then
    ui_print("-> Adding init.jolla-kernel.rc...");
    package_extract_file("jolla-kernel/joan/ramdisk/init.jolla-kernel.rc", "/tmp/ramdisk/init.jolla-kernel.rc");
    set_perm(0, 0, 0755, "/tmp/add_jolla_kernel.sh");
    run_program("/tmp/add_jolla_kernel.sh");
  endif;
  set_perm(0, 0, 0750, "/tmp/ramdisk/init.jolla-kernel.rc");


  ############ Replace Image.gz-dtb ############
  show_progress(0.5, 0.1);
  if file_getprop("/tmp/aroma-data/window2.prop","selected.1")=="1" then
    ui_print("-> Replacing Oreo's joan Image.gz-dtb...");
    package_extract_file("jolla-kernel/joan/Image/O/Image.gz-dtb", "/tmp/split_img/boot.img-zImage");
  endif;


  ############ SOVC ############
  ui_print("-> Setting SOVC...");
  if file_getprop("/tmp/aroma-data/sovc.prop","selected.1")=="1" then
    run_program("/sbin/sh", "-c", "sed -i '/scroff_volctr/d' /tmp/ramdisk/init.jolla-kernel.rc");
  endif;
  if file_getprop("/tmp/aroma-data/sovc.prop","selected.1")=="2" then
    ui_print("-> Turning Off SOVC...");
    package_extract_file("jolla-kernel/joan/sovc/sovc_off_set.sh", "/tmp/sovc_off_set.sh");
    set_perm(0, 0, 0755, "/tmp/sovc_off_set.sh");
    run_program("/tmp/sovc_off_set.sh");
  endif;


  ############ Dynamic fsync ############
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.1.1")=="1" then
    run_program("/sbin/sh", "-c", "sed -i '/Dyn_fsync_active/d' /tmp/ramdisk/init.jolla-kernel.rc");
  endif;
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.1.1")=="0" then
    ui_print("-> Turning Off Dynamic fsync...");
    package_extract_file("jolla-kernel/joan/dynamic_fsync/dyn_fsync_off_set.sh", "/tmp/dyn_fsync_off_set.sh");
    set_perm(0, 0, 0755, "/tmp/dyn_fsync_off_set.sh");
    run_program("/tmp/dyn_fsync_off_set.sh");
  endif;


  ############ Force Advanced Hi-Fi ############
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.1.2")=="0" then
    run_program("/sbin/sh", "-c", "sed -i '/force_advanced_mode/d' /tmp/ramdisk/init.jolla-kernel.rc");
  endif;


  ############ Ramdisk Compress Option ############
  show_progress(0.725, 0.1);
  if file_getprop("/tmp/aroma-data/ramdisk.prop","selected.1")=="1" then
    ui_print("-> Using lz4 compressed ramdisk image...");
    set_perm(0, 0, 0755, "/tmp/lz4.sh");
    run_program("/tmp/lz4.sh");
  endif;
  if file_getprop("/tmp/aroma-data/ramdisk.prop","selected.1")=="2" then
    ui_print("-> Using gzip compressed ramdisk image...");
    set_perm(0, 0, 0755, "/tmp/gzip.sh");
    run_program("/tmp/gzip.sh");
  endif;


  ############ Repack boot.img ############
  show_progress(0.75, 1);
  ui_print("-> Repacking boot.img...");
  run_program("/tmp/repackimg.sh");
  run_program("/tmp/busybox", "dd", "if=/tmp/image-new.img", "of=/dev/block/bootdevice/by-name/boot");


endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="3"

show_progress(1.0, 0);
ui_print("");
ui_print("===================Done==================");
ui_print("=     Kernel Installation Finished!!    =");
ui_print("= Thank you for choosing jolla-kernel!! =");
ui_print("===================Done==================");

run_program("/tmp/busybox", "umount", "/system");
