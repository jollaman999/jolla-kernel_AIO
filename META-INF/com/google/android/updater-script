################################################################### UPDATER SCRIPT #############################################################################

ui_print("======jolla-kernel Installation======");
ui_print("");

ui_print("-> Mounting System & Data...");
run_program("/sbin/busybox", "mount", "/system");
run_program("/sbin/busybox", "mount", "/data");


############################ TWRP Only ################################
if file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="3" then
  ui_print(">>>>>TWRP Only<<<<<");
  ui_print("-> Installing Muti ROM TWRP");
  package_extract_file("jolla-recovery/twrp/recovery.img", "/dev/block/platform/msm_sdcc.1/by-name/recovery");
  package_extract_file("jolla-recovery/twrp/mkdir_multirom.sh", "/tmp/mkdir_multirom.sh");
  package_extract_file("multirom/inst/multirom/trampoline", "/tmp/trampoline");
  set_perm(0, 0, 0777, "/tmp/mkdir_multirom.sh");
  run_program("/tmp/mkdir_multirom.sh");
endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="3"



if file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="3" then

############################ Gproj Kernel ###############################
if file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="1" then

  ui_print(">>>Installing Gproj Kernel<<<");
  
  ui_print("-> Extracting scripts...");
  package_extract_dir("jolla-kernel/gproj/script", "/tmp");
  set_perm(0, 0, 0777, "/tmp/busybox");
  
  ui_print("-> Backup previous prima...");
  set_perm(0, 0, 0777, "/tmp/prima_bakup.sh");
  run_program("/tmp/prima_bakup.sh");
  
  ui_print("-> Removing previous kernel settings...");
  set_perm(0, 0, 0777, "/tmp/cleaner.sh");
  run_program("/tmp/cleaner.sh");
  
  ui_print("-> Extracting Common Files...");
  package_extract_dir("jolla-kernel/gproj/common", "/system");

  ui_print("-> Installing kernel modules...");
  set_perm(0, 0, 0777, "/tmp/modules_remover.sh");
  run_program("/tmp/modules_remover.sh");
  package_extract_dir("jolla-kernel/gproj/modules", "/system/lib/modules");
  
  ui_print("-> Setting prima Permissions...");
  set_perm(0, 0, 0777, "/tmp/prima_set_perm.sh");
  run_program("/tmp/prima_set_perm.sh");

  ui_print("-> Ready to install gproj kernel...");
  package_extract_dir("jolla-kernel/gproj/mkbootimg", "/tmp");
  set_perm(0, 0, 0777, "/tmp/busybox");
  set_perm(0, 0, 0777, "/tmp/unpack-ramdisk.sh");
  set_perm(0, 0, 0777, "/tmp/mk-ramdisk.sh");
  set_perm(0, 0, 0777, "/tmp/mkbootimg.sh");
  set_perm(0, 0, 0777, "/tmp/mkbootimg");
  set_perm(0, 0, 0777, "/tmp/unpackbootimg");


  ############ Unpack boot.img ############
  ui_print("-> Unpacking boot.img...");
  run_program("/tmp/busybox", "dd", "if=/dev/block/platform/msm_sdcc.1/by-name/boot", "of=/tmp/boot.img");
  run_program("/tmp/unpackbootimg", "-i", "/tmp/boot.img", "-o", "/tmp/");
  run_program("/tmp/unpack-ramdisk.sh");


  ############ Add jolla-kernel logo ############
  ui_print("-> Adding jolla-kernel logo...");
  package_extract_file("jolla-kernel/gproj/ramdisk/initlogo.rle", "/tmp/ramdisk/initlogo.rle");
  set_perm(0, 0, 0644, "/tmp/ramdisk/initlogo.rle");


  ############ Replace ramdisk ############
  ui_print(">>>Replacing ramdisk<<<");
  package_extract_dir("jolla-kernel/gproj/ramdisk", "/tmp/ramdisk");
  set_perm(0, 0, 0644, "/tmp/ramdisk/fstab.geehrc");
  set_perm(0, 0, 0750, "/tmp/ramdisk/init.geehrc.rc");


  ############ Replace zImage ############
  ui_print(">>>Replacing zImage<<<");
  package_extract_file("jolla-kernel/gproj/zImage/zImage", "/tmp/zImage");


  ############ Repack boot.img ############
  ui_print("-> Repacking boot.img...");
  run_program("/tmp/mk-ramdisk.sh");
  run_program("/tmp/mkbootimg.sh");
  run_program("/tmp/busybox", "dd", "if=/tmp/newboot.img", "of=/dev/block/platform/msm_sdcc.1/by-name/boot");


  ############ Copy USB-Keyboard.apk ############
  ui_print("-> Copying USB-Keyboard.apk...");
  package_extract_dir("jolla-kernel/app/", "/system/app/");

endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="1"



############################ Multi ROM ############################

######## Install ########
if file_getprop("/tmp/aroma-data/window2.prop","selected.1")=="1" then

if file_getprop("/tmp/aroma-data/checkbox1.prop","item.1.1")=="1" then
ui_print(">>>Installing MultiROM<<<");

show_progress(1.00000, 10);

ui_print("-> Extracting binaries...");
run_program("/sbin/busybox", "mount", "/data");

delete_recursive("/tmp/multirom");
run_program("/sbin/busybox", "mkdir", "/tmp/multirom");
package_extract_dir("multirom/inst/multirom", "/tmp/multirom/");
package_extract_dir("multirom/inst/scripts", "/tmp/");

set_perm(0, 0, 0777, "/tmp/multirom/busybox");
set_perm(0, 0, 0777, "/tmp/bbootimg");
set_perm(0, 0, 0777, "/tmp/extract_multirom.sh");
set_perm(0, 0, 0777, "/tmp/inject_boot.sh");

assert(run_program("/tmp/extract_multirom.sh") == 0);
ui_print("-> Injecting boot image...");
assert(run_program("/tmp/inject_boot.sh") == 0);

ui_print("-> Cleaning up...");
delete_recursive("/tmp/boot");
delete_recursive("/tmp/multirom");
delete("/tmp/bbootimg");
delete("/tmp/bootimg.cfg");
delete("/tmp/initrd.img");
delete("/tmp/zImage");
delete("/tmp/boot.img");
delete("/tmp/dtb.img");
delete("/tmp/second.img");
delete("/tmp/newboot.img");
delete("/tmp/extract_multirom.sh");
delete("/tmp/inject_boot.sh");
delete("/tmp/bootdev");
delete("/tmp/rd_addr");
delete("/tmp/use_mrom_fstab");

ui_print("-> Installing Muti ROM TWRP");
package_extract_file("jolla-recovery/twrp/recovery.img", "/dev/block/platform/msm_sdcc.1/by-name/recovery");

ui_print("-> MultiROM was Installed!!");

endif;
## endif file_getprop("/tmp/aroma-data/checkbox1.prop","item.1.1")=="1"

endif;
## endif file_getprop("/tmp/aroma-data/window2.prop","selected.1")=="1"


######## Uninstall ########
if file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="2" then
ui_print(">>>Uninstalling MultiROM<<<");

ui_print("-> Extracting binaries...");
run_program("/sbin/busybox", "mount", "/data");

package_extract_dir("multirom/uninst/scripts", "/tmp/");

set_perm(0, 0, 0777, "/tmp/busybox");
set_perm(0, 0, 0777, "/tmp/bbootimg");
set_perm(0, 0, 0777, "/tmp/lz4");
set_perm(0, 0, 0777, "/tmp/erase_multirom.sh");
set_perm(0, 0, 0777, "/tmp/erase_multirom_all.sh");
set_perm(0, 0, 0777, "/tmp/clear_boot.sh");

ui_print("-> Removing MultiROM from boot.img...");
assert(run_program("/tmp/clear_boot.sh") == 0);
if file_getprop("/tmp/aroma-data/checkbox2.prop","item.1.1")=="0" then
  ui_print("-> Erasing Multi ROM");
  assert(run_program("/tmp/erase_multirom.sh") == 0);
endif;
if file_getprop("/tmp/aroma-data/checkbox2.prop","item.1.1")=="1" then
  ui_print("-> !!!Erasing Multi ROM with Second Roms!!!");
  assert(run_program("/tmp/erase_multirom_all.sh") == 0);
endif;

ui_print("-> Cleaning up...");
delete_recursive("/tmp/boot");
delete_recursive("/tmp/multirom");
delete("/tmp/lz4");
delete("/tmp/bootimg.cfg");
delete("/tmp/initrd.img");
delete("/tmp/zImage");
delete("/tmp/bbootimg");
delete("/tmp/boot.img");
delete("/tmp/newboot.img");
delete("/tmp/busybox");
delete("/tmp/erase_multirom.sh");
delete("/tmp/erase_multirom_all.sh");
delete("/tmp/clear_boot.sh");
delete("/tmp/bootdev");
delete("/tmp/rd_addr");

ui_print("-> MultiROM was removed!");

endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="2"

endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="3"


############################ OTA Updater ############################
if file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="3" then
  ui_print("-> Installing OTA Updater...");

  delete("/system/kernel.ota.prop");
  delete("/data/kernel.ota.prop");
  delete("/system/jolla-kernel.info");
  delete("/system/bin/jolla-kernel_ota");

  package_extract_file("jolla-kernel/gproj/script/busybox", "/tmp/busybox");
  set_perm(0, 0, 0777, "/tmp/busybox");

  package_extract_dir("ota", "/system");
  set_perm(0, 2000, 0755, "/system/etc/init.d/00jolla-kernel_ota");
  run_program("/tmp/busybox", "chcon", "u:object_r:system_file:s0", "/system/etc/init.d/00jolla-kernel_ota");
  package_extract_file("jolla-kernel.info", "/system/jolla-kernel.info");
  package_extract_file("jolla-kernel.info", "/data/kernel.ota.prop");
  run_program("/tmp/busybox", "ln", "-s", "/data/kernel.ota.prop", "/system/kernel.ota.prop");
endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="3"

ui_print("");
ui_print("====================Done===================");
ui_print("==     Kernel Installation Finished!!    ==");
ui_print("== Thank you for choosing jolla-kernel!! ==");
ui_print("====================Done===================");

run_program("/sbin/busybox", "umount", "/system");
run_program("/sbin/busybox", "umount", "/data");

show_progress(1, 0);
