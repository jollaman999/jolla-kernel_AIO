################################################################### UPDATER SCRIPT #############################################################################

ui_print("======jolla-kernel Installation======");
ui_print("");

ui_print("-> Mounting System & Data...");
run_program("/sbin/busybox", "umount", "/system");
run_program("/sbin/busybox", "umount", "/data");
run_program("/sbin/busybox", "mount", "/system");
run_program("/sbin/busybox", "mount", "/data");


############################ TWRP Only ################################
if file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="3" then
  ui_print(">>>>>TWRP Only<<<<<");
  ui_print("-> Installing TWRP");
  show_progress(0.5, 1);
  package_extract_file("jolla-recovery/twrp/recovery.img", "/dev/block/platform/soc.0/f9824900.sdhci/by-name/recovery");
endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="3"



if file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="3" then

  ############################ bullhead Kernel ###############################
  ui_print(">>>Installing bullhead Kernel<<<");

  show_progress(0.1, 0.1);
  ui_print("-> Cleaning previous installations...");
  package_extract_file("cleaner.sh", "/tmp/cleaner.sh");
  set_perm(0, 0, 0777, "/tmp/cleaner.sh");
  run_program("/tmp/cleaner.sh");

  show_progress(0.2, 0.1);
  ui_print("-> Ready to install bullhead kernel...");
  package_extract_dir("jolla-kernel/bullhead/mkbootimg", "/tmp");
  set_perm(0, 0, 0755, "/tmp/bin/busybox");
  set_perm(0, 0, 0755, "/tmp/multirom_hack.sh");
  set_perm(0, 0, 0755, "/tmp/repackimg.sh");
  set_perm(0, 0, 0755, "/tmp/unpackimg.sh");


  ############ Unpack boot.img ############
  show_progress(0.4, 1);
  if file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="2" then
  ui_print("-> Unpacking boot.img...");
  run_program("/tmp/bin/busybox", "dd", "if=/dev/block/platform/soc.0/f9824900.sdhci/by-name/boot", "of=/tmp/boot.img");
  run_program("/tmp/unpackimg.sh");
  endif;
  ## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="2"


  ######## Multi ROM Uninstall ########
  show_progress(0.5, 1);
  if file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="2" then
    ui_print(">>>Uninstalling MultiROM<<<");

    ui_print("-> Unpacking /sdcard/boot.img_bak...");
    run_program("/tmp/bin/busybox", "cp", "/sdcard/boot.img_bak", "/tmp/boot.img");
    run_program("/tmp/unpackimg.sh");

    package_extract_dir("jolla-kernel/bullhead/multirom/uninst", "/tmp/");
    set_perm(0, 0, 0755, "/tmp/clear_ramdisk.sh");
    set_perm(0, 0, 0755, "/tmp/erase_multirom.sh");
    set_perm(0, 0, 0755, "/tmp/erase_multirom_all.sh");

    ui_print("-> Removing MultiROM from ramdisk...");
    assert(run_program("/tmp/clear_ramdisk.sh") == 0);
    if file_getprop("/tmp/aroma-data/checkbox3.prop","item.1.1")=="0" then
      ui_print("-> Erasing Multi ROM");
      assert(run_program("/tmp/erase_multirom.sh") == 0);
    endif;
    if file_getprop("/tmp/aroma-data/checkbox3.prop","item.1.1")=="1" then
      ui_print("-> !!!Erasing Multi ROM with Second Roms!!!");
      assert(run_program("/tmp/erase_multirom_all.sh") == 0);
    endif;

    ui_print("-> MultiROM was removed!");

  endif;
  ## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")=="2"


  ############ Replace ramdisk ############
  show_progress(0.6, 1);
  ui_print("-> Replacing ramdisk...");
  package_extract_file("jolla-kernel/bullhead/ramdisk/fstab.bullhead", "/tmp/ramdisk/fstab.bullhead");
  package_extract_file("jolla-kernel/bullhead/ramdisk/init.bullhead.rc", "/tmp/ramdisk/init.bullhead.rc");
  package_extract_file("jolla-kernel/bullhead/ramdisk/init.superuser.rc", "/tmp/ramdisk/init.superuser.rc");
  package_extract_file("jolla-kernel/bullhead/ramdisk/jolla-kernel.prop", "/tmp/ramdisk/jolla-kernel.prop");
  set_perm(0, 0, 0644, "/tmp/ramdisk/fstab.bullhead");
  set_perm(0, 0, 0750, "/tmp/ramdisk/init.bullhead.rc");
  set_perm(0, 0, 0750, "/tmp/ramdisk/init.superuser.rc");
  set_perm(0, 0, 0644, "/tmp/ramdisk/jolla-kernel.prop");
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.1.1")=="1" then
      ui_print("-> Patching sepolicy...");
      package_extract_file("jolla-kernel/bullhead/ramdisk/sepolicy", "/tmp/ramdisk/sepolicy");
      set_perm(0, 0, 0644, "/tmp/ramdisk/sepolicy");
  endif;


  ############ Replace Image.gz-dtb ############
  show_progress(0.7, 1);
  if file_getprop("/tmp/aroma-data/window2.prop","selected.1")=="1" then
    ui_print("-> Replacing M's bullhead Image.gz-dtb...");
    package_extract_file("jolla-kernel/bullhead/Image/m/Image.gz-dtb", "/tmp/split_img/boot.img-zImage");
  endif;
  if file_getprop("/tmp/aroma-data/window2.prop","selected.1")=="2" then
    ui_print("-> Replacing N's bullhead Image.gz-dtb...");
    package_extract_file("jolla-kernel/bullhead/Image/n/Image.gz-dtb", "/tmp/split_img/boot.img-zImage");
  endif;


  ############ Ramdisk Compress Option ############
  show_progress(0.75, 3);
  if file_getprop("/tmp/aroma-data/ramdisk.prop","selected.1")=="1" then
    ui_print("-> Using gzip compressed ramdisk image...");
    set_perm(0, 0, 0755, "/tmp/gzip.sh");
    run_program("/tmp/gzip.sh");
  endif;
  if file_getprop("/tmp/aroma-data/ramdisk.prop","selected.1")=="2" then
    ui_print("-> Using lz4 compressed ramdisk image...");
    set_perm(0, 0, 0755, "/tmp/lz4.sh");
    run_program("/tmp/lz4.sh");
  endif;


  ############ init.d Patch ############
  show_progress(0.8, 1);
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.4.1")=="1" then
    ui_print("-> Patching init.d...");
    set_perm(0, 0, 0755, "/tmp/init.d_support.sh");
    run_program("/tmp/init.d_support.sh");
    package_extract_dir("jolla-kernel/bullhead/etc/", "/system/etc/");
    set_perm(0, 2000, 0755, "/system/etc/init.d/77jolla-kernel");
  endif;


  ############ Patch F2FS binaries ############
  show_progress(0.81, 1);
  if file_getprop("/tmp/aroma-data/f2fs.prop","item.1.1")=="1" then
    ui_print("-> Patching F2FS binaries...");
    package_extract_dir("jolla-kernel/bullhead/f2fs/system/", "/system/");
    set_perm(0, 0, 0755, "/system/bin/fsck.f2fs");
    set_perm(0, 0, 0755, "/system/bin/mkfs.f2fs");
    package_extract_file("jolla-kernel/bullhead/f2fs/make_symlinks.sh", "/tmp/make_symlinks.sh");
    set_perm(0, 0, 0755, "/tmp/make_symlinks.sh");
    run_program("/tmp/make_symlinks.sh");
  endif;
  ## endif file_getprop("/tmp/aroma-data/f2fs.prop","item.1.1")=="1" then


  ############ Turn Off SOVC ############
  show_progress(0.825, 1);
  ui_print("-> Setting SOVC...");
  delete("/system/etc/init.d/77sovc_auto_off");

  if file_getprop("/tmp/aroma-data/sovc.prop","selected.1")=="1" then
    delete("/system/etc/init.d/77sovc_off");
    if file_getprop("/tmp/aroma-data/sovc_auto_off.prop","selected.1")!="1" then
      set_perm(0, 0, 0755, "/tmp/init.d_support.sh");
      run_program("/tmp/init.d_support.sh");
      package_extract_dir("jolla-kernel/bullhead/etc/", "/system/etc/");
      set_perm(0, 2000, 0755, "/system/etc/init.d/77jolla-kernel");

      package_extract_file("jolla-kernel/bullhead/sovc/77sovc_auto_off", "/system/etc/init.d/77sovc_auto_off");
      set_perm(0, 2000, 0755, "/system/etc/init.d/77sovc_auto_off");
      package_extract_file("jolla-kernel/bullhead/sovc/sovc_auto_off_set.sh", "/tmp/sovc_auto_off_set.sh");
      set_perm(0, 0, 0755, "/tmp/sovc_auto_off_set.sh");

      if file_getprop("/tmp/aroma-data/sovc_auto_off.prop","selected.1")=="2" then
        run_program("/tmp/sovc_auto_off_set.sh", "5000");
      endif;
      if file_getprop("/tmp/aroma-data/sovc_auto_off.prop","selected.1")=="3" then
        run_program("/tmp/sovc_auto_off_set.sh", "6000");
      endif;
      if file_getprop("/tmp/aroma-data/sovc_auto_off.prop","selected.1")=="4" then
        run_program("/tmp/sovc_auto_off_set.sh", "7000");
      endif;
      if file_getprop("/tmp/aroma-data/sovc_auto_off.prop","selected.1")=="5" then
        run_program("/tmp/sovc_auto_off_set.sh", "8000");
      endif;
      if file_getprop("/tmp/aroma-data/sovc_auto_off.prop","selected.1")=="6" then
        run_program("/tmp/sovc_auto_off_set.sh", "10000");
      endif;
    endif;
  endif;

  if file_getprop("/tmp/aroma-data/sovc.prop","selected.1")=="2" then
    ui_print("-> Turning Off SOVC...");
    if file_getprop("/tmp/aroma-data/checkbox1.prop","item.4.1")!="1" then
      set_perm(0, 0, 0755, "/tmp/init.d_support.sh");
      run_program("/tmp/init.d_support.sh");
      package_extract_dir("jolla-kernel/bullhead/etc/", "/system/etc/");
      set_perm(0, 2000, 0755, "/system/etc/init.d/77jolla-kernel");
    endif;
    package_extract_file("jolla-kernel/bullhead/sovc/77sovc_off", "/system/etc/init.d/77sovc_off");
    set_perm(0, 2000, 0755, "/system/etc/init.d/77sovc_off");
  endif;


  ############ kcal_fix_yellow ############
  delete("/system/etc/init.d/77kcal_fix_yellow_off");
  if file_getprop("/tmp/aroma-data/kcal.prop","selected.1")=="2" then
    ui_print("-> Setting KCAL...");
    if file_getprop("/tmp/aroma-data/checkbox1.prop","item.4.1")!="1" then
      set_perm(0, 0, 0755, "/tmp/init.d_support.sh");
      run_program("/tmp/init.d_support.sh");
      package_extract_dir("jolla-kernel/bullhead/etc/", "/system/etc/");
      set_perm(0, 2000, 0755, "/system/etc/init.d/77jolla-kernel");
    endif;
    package_extract_file("jolla-kernel/bullhead/kcal/77kcal_fix_yellow_off", "/system/etc/init.d/77kcal_fix_yellow_off");
    set_perm(0, 2000, 0755, "/system/etc/init.d/77kcal_fix_yellow_off");
  endif;


  ############ Repack boot.img ############
  show_progress(0.85, 1);
  ui_print("-> Repacking boot.img...");
  run_program("/tmp/repackimg.sh");
  run_program("/tmp/bin/busybox", "dd", "if=/tmp/image-new.img", "of=/dev/block/platform/soc.0/f9824900.sdhci/by-name/boot");


  ############ Patch Charging LED ############
  show_progress(0.875, 1);
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.2.1")=="1" then
    ui_print("-> Patching charging LED...");
    package_extract_dir("jolla-kernel/bullhead/led/system/", "/system/");
  endif;
  ## endif file_getprop("/tmp/aroma-data/checkbox1.prop","item.2.1")=="1" then


  ############ Copy Applications ############
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.3.1")=="1" then
    show_progress(0.9, 1);
    ui_print("-> Copying jolla-kernel Updater...");
    package_extract_dir("updater/", "/data/app/");
    run_program("/sbin/busybox", "chown", "-R", "system.system", "/data/app/com.jollakernelupdater-1/");
  endif;
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.3.2")=="1" then
    show_progress(0.925, 1);
    ui_print("-> Copying A2DPChecker...");
    package_extract_dir("jolla-kernel/app/A2DPChecker/", "/data/app/");
    run_program("/sbin/busybox", "chown", "-R", "system.system", "/data/app/com.jollaman999.bluetootha2dp-1/");
  endif;
  if file_getprop("/tmp/aroma-data/checkbox1.prop","item.3.3")=="1" then
    show_progress(0.95, 1);
    ui_print("-> Copying USB-Keyboard...");
    package_extract_dir("jolla-kernel/app/USB-Keyboard/", "/data/app/");
    run_program("/sbin/busybox", "chown", "-R", "system.system", "/data/app/remote.hid.keyboard.client-1/");
  endif;


  ######## Multi ROM Install ########
  if file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="2" then

  if file_getprop("/tmp/aroma-data/checkbox2.prop","item.1.1")=="1" then
    show_progress(0.96, 10);
    ui_print(">>> Installing MultiROM <<<");

    delete_recursive("/tmp/multirom");
    run_program("/sbin/busybox", "mkdir", "/tmp/multirom");
    package_extract_dir("jolla-kernel/bullhead/multirom/inst/multirom", "/tmp/multirom/");
    package_extract_dir("jolla-kernel/bullhead/multirom/inst/scripts", "/tmp/");

    set_perm(0, 0, 0777, "/tmp/multirom/busybox");
    set_perm(0, 0, 0777, "/tmp/bbootimg");
    set_perm(0, 0, 0777, "/tmp/extract_multirom.sh");
    set_perm(0, 0, 0777, "/tmp/inject_boot.sh");

    assert(run_program("/tmp/extract_multirom.sh") == 0);
    ui_print("-> Injecting boot image...");
    assert(run_program("/tmp/inject_boot.sh") == 0);

    ui_print("-> Cleaning up...");
    delete_recursive("/tmp/boot");
    delete_recursive("/tmp/multirom");
    delete("/tmp/bbootimg");
    delete("/tmp/bootimg.cfg");
    delete("/tmp/initrd.img");
    delete("/tmp/zImage");
    delete("/tmp/boot.img");
    delete("/tmp/dtb.img");
    delete("/tmp/second.img");
    delete("/tmp/newboot.img");
    delete("/tmp/extract_multirom.sh");
    delete("/tmp/inject_boot.sh");
    delete("/tmp/bootdev");
    delete("/tmp/rd_addr");
    delete("/tmp/use_mrom_fstab");

    ui_print("-> Installing Muti ROM TWRP...");
    package_extract_file("jolla-recovery/twrp/recovery.img", "/dev/block/platform/soc.0/f9824900.sdhci/by-name/recovery");

    ui_print("-> Copying Multi ROM Manager...");
    package_extract_dir("jolla-kernel/bullhead/multirom/app/", "/data/app/");
    run_program("/sbin/busybox", "chown", "-R", "system.system", "/data/app/com.tassadar.multirommgr-1/");

    ui_print("-> MultiROM was Installed!!");

  endif;
  ## endif file_getprop("/tmp/aroma-data/checkbox2.prop","item.1.1")=="1"

  endif;
  ## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="2"

  ######## Multi ROM Hack ########
  ui_print("-> Checking Multi ROM state...");
  run_program("/tmp/multirom_hack.sh");
  if file_getprop("/tmp/aroma-data/multirom.prop","multirom")=="1" then
    ui_print(">>> THIS IS MULTIROM <<<");
    ui_print("-> Copying Multi ROM Manager...");
    package_extract_dir("jolla-kernel/bullhead/multirom/app/", "/data/app/");
    run_program("/sbin/busybox", "chown", "-R", "system.system", "/data/app/com.tassadar.multirommgr-1/");
  endif;
  ## endif file_getprop("/tmp/aroma-data/multirom.prop","multirom")=="1"

endif;
## endif file_getprop("/tmp/aroma-data/window1.prop","selected.1")!="3"

show_progress(1.0, 0);
ui_print("");
ui_print("====================Done===================");
ui_print("==     Kernel Installation Finished!!    ==");
ui_print("== Thank you for choosing jolla-kernel!! ==");
ui_print("====================Done===================");

run_program("/sbin/busybox", "umount", "/system");
run_program("/sbin/busybox", "umount", "/data");
